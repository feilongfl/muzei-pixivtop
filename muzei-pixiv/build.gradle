/*
 * Copyright 2018 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def buildVersion = 1
def version = "v1.0.0"

def cmdGetTagname = 'git describe --tags'
def cmdGetCommitNums = 'git rev-list HEAD --count'
if(System.getenv("TRAVIS_BUILD_NUMBER") != null){
    buildVersion = System.getenv("TRAVIS_BUILD_NUMBER").trim().toInteger()
} else {
    buildVersion = cmdGetCommitNums.execute().text.trim().toInteger()
}
version = cmdGetTagname.execute().text.trim()

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "android.arch.work:work-runtime-ktx:$workManagerVersion"
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.5.0"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.google.android.apps.muzei:muzei-api:3.1.0"
//    implementation "com.google.android.apps.muzei:muzei-api:+"
//    implementation project(':muzei-api')
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    defaultConfig {
        minSdkVersion 19
        versionCode buildVersion
        versionName version
        targetSdkVersion rootProject.ext.targetSdkVersion

        def unsplashAuthorityValue = "com.muzei.feilong.pixiv"
        manifestPlaceholders = [unsplashAuthority: unsplashAuthorityValue]
        buildConfigField "String", "PIXIV_TOP", "\"${unsplashAuthorityValue}\""
    }
    signingConfigs {
        release {
            storeFile file("muzei-pixivtop.jks")
            storePassword System.getenv("KEYSTORE_PASS")
            keyAlias System.getenv("ALIAS_NAME")
            keyPassword System.getenv("ALIAS_PASS")
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        android.applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "muzei-pixiv.apk"
            }
        }
    }
}
